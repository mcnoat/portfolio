#AwardWinners
select ?filmLabel (YEAR(?date) as ?year) ?directorLabel ?genderLabel ?duration ?countryLabel ?countryCode ?film
where 
{
  ?film wdt:P31 wd:Q11424. #is a film

  ?film p:P166 ?awardStatement . # won a given award
  ?awardStatement ps:P166 wd:$award .
  ?awardStatement pq:P585 ?date .

  optional {?film wdt:P57 ?director.}
  optional {?director wdt:P21 ?gender.}
  optional {?film p:P2047 ?durationStatement.} # this gets us into the "duration box" of the Wikidate page for the film
  optional {?durationStatement ps:P2047 ?duration.}
            ?durationStatement a wikibase:BestRank . # this gets information out of the "duration box"
  optional {?durationStatement pq:P518 ?version.} # this gets information out of the "duration box"
  filter(!bound(?version) || ?version in (wd:Q26225765)) # select theatrical version
  optional {?film wdt:P495 ?country.}
  optional {?country wdt:P298 ?countryCode.}

  service wikibase:label {
    bd:serviceParam wikibase:language "en".
    ?film rdfs:label ?filmLabel.
    ?director rdfs:label ?directorLabel.
    ?gender rdfs:label ?genderLabel.
    ?country rdfs:label ?countryLabel.
  }
}
order by (?year)